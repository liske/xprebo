#!/usr/bin/perl

# xprebo - XMPP Presence Bot
#
# Authors:
#   Thomas Liske <thomas@fiasko-nw.net>
#
# Copyright Holder:
#   2011 - 2012 (C) Thomas Liske <thomas@fiasko-nw.net>
#
# License:
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this package; if not, write to the Free Software
#   Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301 USA
#

use Config;
use File::Basename;
use Getopt::Std;
use Log::Dispatch;
use Log::Dispatch::Screen;
use Log::Dispatch::Syslog;
use Log::Dispatch::File;
use POSIX qw(strftime mkfifo);
use Proc::Daemon;
use Proc::PID::File;
use Net::XMPP;
use IO::Select;
use IO::Handle;

use strict;
use warnings;

$0 = 'xprebod';

use constant {
    XPREBOD_VERSION => '0.1',

    XML_IQ_VERSION =>
"<query xmlns='jabber:iq:version'>
    <name>xprebod</name>
    <version>%NAJVER%</version>
</query>",

    XML_IQ_NOVCARD =>
"<vCard xmlns='vcard-temp'/>",

    XML_IQ_DISCO =>
"<query xmlns='http://jabber.org/protocol/disco#info'>
    <feature var='jabber:iq:last'/>
    <feature var='jabber:iq:version'/>
    <feature var='urn:xmpp:ping'/>
    <feature var='urn:xmpp:time'/>
    <feature var='vcard-temp'/>
</query>",

    XML_IQ_NOTIMPL =>
"<error type='cancel'>
    <feature-not-implemented xmlns='urn:ietf:params:xml:ns:xmpp-stanzas'/>
</error>",
};

my $logfmt = sub { my %p = @_; chomp($p{'message'}); return ("[$p{level}] $p{message}\n"); };
my $logffile = sub { my %p = @_;  return (scalar localtime())." [$$] $p{message}"; };
my $logger = Log::Dispatch->new(callbacks => $logfmt);

my $daemonize;
END {
    $logger->notice('terminated') if(defined($logger) && !defined($daemonize));
}

$logger->add(
    Log::Dispatch::Screen->new(
	name => 'console',
	min_level => 'notice',
    )
);

sub mydie($) {
    $logger->critical(shift);
    exit 1;
}

our $opt_c = '/etc/xprebo/xprebo.conf';
our $opt_f;
getopts('fc:');
mydie("Could not read config file '$opt_c'.") unless(-r $opt_c);
$logger->info("config file: '$opt_c'");

my %xmpp_conf;
my %auth_conf;
my %xprebo_conf;
my %xprebo_acls;
my %xprebo_cmds = (
    help => {
	role => 'user',
	preq => [],
	popt => [],
	descr => 'show command list',
	sub => \&SendHelp,
    },
);
eval `cat "$opt_c"`;

# enable log to file
if(exists($xprebo_conf{'logfile'})) {
    $logger->add(
	Log::Dispatch::File->new(
	    name => 'logfile',
	    min_level => 'info',
	    filename => $xprebo_conf{'logfile'},
	    close_after_write => '1',
	    callbacks => $logffile,
	)
    );
}
# fallback to syslog
else {
    $logger->add(
	Log::Dispatch::Syslog->new(
	    name => 'syslog',
	    min_level => 'info',
	    ident => 'xprebod',
	    logopt => 'pid',
	    facility => 'daemon',
	)
    );
}

$logger->info('xprebod '.XPREBOD_VERSION);

my $conn;
my $last_change = time();

# send vCard IQ
sub SendVCard(%) {
    my $iq = new Net::XMPP::IQ();
    $iq->SetIQ(@_);

    if (exists($xprebo_conf{'vcard'}) &&
        defined($xprebo_conf{'vcard'}) &&
        (-r $xprebo_conf{'vcard'}) &&
        open(FILEREAD, '<', $xprebo_conf{'vcard'})) {

        $iq->InsertRawXML(<FILEREAD>);
        close(FILEREAD);
    }
    else {
        $iq->InsertRawXML(XML_IQ_NOVCARD);
    }

    $conn->Send($iq);
}


# setup named pipes
$logger->info("status named pipe : $xprebo_conf{status_pipe}");
$logger->info("message named pipe: $xprebo_conf{show_pipe}");

unless(-p $xprebo_conf{'status_pipe'}) {
    unlink($xprebo_conf{'status_pipe'});
    mkfifo($xprebo_conf{'status_pipe'}, 0770) || mydie "Failed to setup fifo $xprebo_conf{status_pipe}: $!";
}
open(FIFO_STATUS, '<', $xprebo_conf{'status_pipe'}) || mydie "Failed to open fifo $xprebo_conf{status_pipe}: $!";

unless(-p $xprebo_conf{'show_pipe'}) {
    unlink($xprebo_conf{'show_pipe'});
    mkfifo($xprebo_conf{'show_pipe'}, 0770) || mydie "Failed to setup fifo $xprebo_conf{show_pipe}: $!";
}
open(FIFO_SHOW, '<', $xprebo_conf{'show_pipe'}) ||  mydie "Failed to open fifo $xprebo_conf{show_pipe}: $!";

my $select = IO::Select->new();
$select->add(\*FIFO_STATUS);
$select->add(\*FIFO_SHOW);

unless($opt_f) {
    # daemonize
    $daemonize = 1;
    eval { Proc::Daemon::Init() };
    $daemonize = undef;
    mydie "Unable to fork daemon: $@" if($@);

    # check if we are alone
    eval {
	die "Daemon is already running or PID file is stale!" if(Proc::PID::File->running(verify => 1, dir => $xprebo_conf{'rundir'}));
    };
    mydie "Failed to setup PID file: $@" if($@);

    # detach log to console
    $logger->notice('Could not remove console dispatcher!') unless($logger->remove('console'));
}

my $presence_status = 'Initializing...';
my $presence_show = 'dnd';
while(1) {
    $conn = new Net::XMPP::Client;

    # register callbacks
    $conn->SetCallBacks(message=>\&InMessage, iq=>\&InIQ);

    # try to connect
    unless($conn->Connect(%xmpp_conf)) {
        $logger->warning("Could not connect to XMPP server ($!), sleeping 30 seconds!");
        sleep(30);
        next;
    }
    $logger->info('XMPP server connected');

    # try to auth
    my @result = $conn->AuthSend(%auth_conf);
    unless($result[0] eq 'ok') {
        $logger->warning("Auth failed ($result[1]), sleeping 60 seconds!");
        sleep(60);
        next;
    }
    $logger->info('XMPP auth ok');

    $conn->PresenceSend(
	status => $presence_status,
	show => $presence_show,
    );
    SendVCard(type => 'set');

    # wait for XMPP messages and nagios status updates
    while(defined($conn->Process(3))) {
	my @ready = $select->can_read(0.1);
    }

    $presence_status = 'XMPP connection broken...';
    $presence_show = 'dnd';

    $logger->warning('XMPP connection broken, sleeping 30 seconds!');
    sleep(30);
}


sub SendHelp($@) {
    my $message = shift;
    my $params = shift;
    my $roles = shift;

    my $help;
    foreach my $cmd (sort keys %xprebo_cmds) {
	next unless(scalar grep {$_ eq ${$xprebo_cmds{$cmd}}{'role'}} @{$roles});

	$help .= "\n".${$xprebo_cmds{$cmd}}{'descr'}.":\n\t$cmd";

	$help .= ' <'.join('> <', @{${$xprebo_cmds{$cmd}}{'preq'}}).'>'
	    unless($#{${$xprebo_cmds{$cmd}}{'preq'}} == -1);

	$help .= ' ['.join('] [', @{${$xprebo_cmds{$cmd}}{'popt'}}).']'
	    unless($#{${$xprebo_cmds{$cmd}}{'popt'}} == -1);

	$help .= "\n";
    }

    if($help) {
	$conn->Send($message->Reply(
	    body => "Command list:\n$help",
	));
    }
    else {
	$conn->Send($message->Reply(
	    body => "You don't have the required permission!",
	    errorcode => 403,
	    error => 'Forbidden',
	    type => 'error',
	));
    }
}

# we've got a message
sub InMessage {
    my $sid = shift;
    my $message = shift;

    if($message->GetType() eq 'error') {
	$logger->info(
	    'Got error '.$message->GetErrorCode().' from '.
	    $message->GetFrom().': '.$message->GetError());

	return;
    }

    my $reply = $message->Reply();
    my ($cmd, @params) = split(/\s+/, $message->GetBody());

    my @roles = qw(any);

    foreach my $role (keys %xprebo_acls) {
	push(@roles, $role) if($message->GetFrom() =~ /$xprebo_acls{$role}/);
    }

    my $user = 'JID: '.$message->GetFrom().', roles: '.join(', ', @roles);

    if(exists($xprebo_cmds{$cmd})) {
	if (scalar grep {$_ eq ${$xprebo_cmds{$cmd}}{'role'}} @roles) {
	    if(exists(${$xprebo_cmds{$cmd}}{'run'})) {
		my $body = '';
		$body = `${$xprebo_cmds{$cmd}}{'run'}`
		    if (-x ${$xprebo_cmds{$cmd}}{'run'});
		chomp($body);

		if($body ne '') {
		    $logger->info("command ($user): $cmd");

		    $conn->Send($message->Reply(body=> "\n$body\n"));
		}
		else {
		    $logger->notice("command binary failed ($user): $cmd");

		    $conn->Send($message->Reply(
			body => 'Command failed!',
			errorcode => 500,
			error => 'Internal Error',
			type => 'error',
		    ));
		}
	    }
	    elsif(exists(${$xprebo_cmds{$cmd}}{'sub'})) {
		&{${$xprebo_cmds{$cmd}}{'sub'}}($message, \@params, \@roles);
	    }
	    else {
		$logger->notice("command not implemented ($user): $cmd");

		$conn->Send($message->Reply(
		    body => 'Command not implemented!',
		    errorcode => 500,
		    error => 'Internal Error',
		    type => 'error',
		));
	    }
	}
	else {
	    $logger->notice("command denied ($user): $cmd");

	    $conn->Send($message->Reply(
		body => "You don't have the required permission for the '$cmd' command!",
		errorcode => 403,
		error => 'Forbidden',
		type => 'error',
	    ));
	}
    }
    else {
	if (exists($xprebo_cmds{'help'}) && scalar grep {$_ eq ${$xprebo_cmds{'help'}}{'role'}} @roles) {
	    $logger->info("command unknown ($user): $cmd");

	    $conn->Send($message->Reply(
		body => "Unknown command '$cmd' - try 'help' to get command list!",
		errorcode => 404,
		error => 'Not found',
		type => 'error',
	    ));
	}
	else {
	    $conn->Send($message->Reply(
		body => "Access denied!",
		errorcode => 403,
		error => 'Forbidden',
		type => 'error',
	    ));
	}
    }
}

# somebody query's an IQ
sub InIQ {
    my $sid = shift;
    my $iq = shift;

    my $from = $iq->GetFrom();
    my $to = $iq->GetTo();
    my $id = $iq->GetID();
    my $type = $iq->GetType();
    my $query = $iq->GetQuery();

    if($type eq 'get') {
        my $xmlns = $query->GetXMLNS();

        if ($xmlns eq 'jabber:iq:version') {
            my $iq = new Net::XMPP::IQ();
            $iq->SetIQ(to => $from, from => $to, id => $id, type => 'result');

            my $ver = XML_IQ_VERSION;
            my $i;
            $ver =~ s/%NAJVER%/${\(XPREBOD_VERSION)}/;

            $iq->InsertRawXML($ver);
            $conn->Send($iq);
        }
        elsif ($xmlns eq 'jabber:iq:last') {
            my $iq = new Net::XMPP::IQ();
            $iq->SetIQ(to => $from, from => $to, id => $id, type => 'result');

            my $seconds = "seconds='".(time() - $last_change)."'";

            $iq->InsertRawXML("<query xmlns='jabber:iq:last' $seconds/>");
            $conn->Send($iq);
        }
        elsif ($xmlns eq 'urn:xmpp:time') {
            my $iq = new Net::XMPP::IQ();
            $iq->SetIQ(to => $from, from => $to, id => $id, type => 'result');

            my $tzo = strftime('%z', localtime);
            $tzo =~ s/(.+)(..)/$1:$2/;
            my $utc = strftime('%FT%TZ', gmtime);
            $iq->InsertRawXML(
"<time xmlns='urn:xmpp:time'>
    <tzo>$tzo</tzo>
    <utc>$utc</utc>
</time>"
            );
            $conn->Send($iq);
        }
        elsif ($xmlns eq 'vcard-temp') {
            SendVCard(
                id => $id,
                type => 'result',
                to => $from,
                from => $to,
            );
        }
        elsif ($xmlns eq 'http://jabber.org/protocol/disco#info') {
            my $iq = new Net::XMPP::IQ();
            $iq->SetIQ(to => $from, from => $to, id => $id, type => 'result');
            $iq->InsertRawXML(XML_IQ_DISCO);
            $conn->Send($iq);
        }
        elsif ($xmlns eq 'urn:xmpp:ping') {
            my $iq = new Net::XMPP::IQ();
            $iq->SetIQ(to => $from, from => $to, id => $id, type => 'result');
            $conn->Send($iq);
        }
        else {
            my $iq = new Net::XMPP::IQ();
            $iq->SetIQ(to => $from, from => $to, id => $id, type => 'error');
            $iq->InsertRawXML(XML_IQ_NOTIMPL);
            $conn->Send($iq);

            $logger->notice("Unhandled IQ: $xmlns");
        }
    }
}
